apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

buildscript {
    repositories {
        jcenter()
        maven {
            url "https://maven.google.com"
        }
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
    }
}

// Variables
int libraryVersionCode = 11
String libraryVersion = '1.3.0'
String libraryGroup = 'com.crowdfire.cfalertdialog'

String gitUrl = 'https://github.com/Codigami/CFAlertDialog.git'
String siteUrl = 'https://github.com/Codigami/CFAlertDialog'

// Android settings
android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        versionCode libraryVersionCode
        versionName libraryVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

// Dependancies
dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.1-alpha01', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha01'
    implementation 'androidx.cardview:cardview:1.0.0'
    testImplementation 'junit:junit:4.12'
}

// Local properties
Properties properties = new Properties()
properties.load(project.rootProject.file('bintray.properties').newDataInputStream())

group = libraryGroup
version = libraryVersion

// Bintray upload tasks
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']

    pkg {
        repo = 'cfalertdialog'
        name = 'cfalertdialog'
        userOrg = 'crowdfire'
        licenses = ['Apache-2.0']
        vcsUrl = gitUrl
        publish = true
        version{
            name = libraryVersion
            desc = 'Development release v0.9'
            released  = new Date()
            vcsTag = libraryVersion
            gpg {
                sign = true
                passphrase = properties.getProperty("bintray.gpg.password")
            }
        }
    }
}

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                groupId libraryGroup
                artifactId 'cfalertdialog'

                name 'CFAlertDialog'
                description 'A customisable alert dialog for Android'
                url siteUrl

                developers {
                    developer {
                        id properties.getProperty("bintray.developer.id")
                        name properties.getProperty("bintray.developer.name")
                    }
                }

                scm {
                    url siteUrl
                    connection gitUrl
                    developerConnection gitUrl
                }

                licenses {
                    license {
                        name 'Apache-2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
            }
        }
    }
}
